FIND_PKGLIBRARIES(LIBSSH2 libssh2)

IF(USE_SSH STREQUAL ON)
	IF(LIBSSH2_FOUND)
		SET(SSH_BACKEND "libssh2")
	ELSE()
		MESSAGE(STATUS "Unable to autodetect usable SSH backend.")
		SET(SSH_BACKEND "OFF")
	ENDIF()
ELSEIF(USE_SSH)
	SET(SSH_BACKEND ${USE_SSH})
ELSE()
	SET(SSH_BACKEND OFF)
ENDIF()

IF(SSH_BACKEND)
	IF(SSH_BACKEND STREQUAL "libssh2")
		IF(NOT LIBSSH2_FOUND)
			MESSAGE(FATAL_ERROR "Asked for system libssh2, but it wasn't found")
		ENDIF()

		LIST(APPEND LIBGIT2_SYSTEM_INCLUDES ${LIBSSH2_INCLUDE_DIRS})
		LIST(APPEND LIBGIT2_LIBS ${LIBSSH2_LIBRARIES})
		LIST(APPEND LIBGIT2_PC_LIBS ${LIBSSH2_LDFLAGS})

		CHECK_LIBRARY_EXISTS("${LIBSSH2_LIBRARIES}" libssh2_userauth_publickey_frommemory
				     "${LIBSSH2_LIBRARY_DIRS}" HAVE_LIBSSH2_MEMORY_CREDENTIALS)
		IF (HAVE_LIBSSH2_MEMORY_CREDENTIALS)
			SET(GIT_SSH_MEMORY_CREDENTIALS 1)
		ENDIF()

		SET(BACKEND "system libssh2")
	ELSEIF(SSH_BACKEND STREQUAL "libssh2_embedded")
		SET(LIBSSH2_EMBEDDED_PATH "" CACHE STRING "Path to libssh2 to embed")
		IF(LIBSSH2_EMBEDDED_PATH STREQUAL "")
			MESSAGE(FATAL_ERROR "Asked for embedded libssh2 backend, but no path was set. "
					    "Please set LIBSSH2_EMBEDDED_PATH")
		ENDIF()

		SET(BUILD_TESTING OFF CACHE BOOL "Build testing code for embedded libssh2")
		SET(BUILD_SHARED_LIBS_OLD ${BUILD_SHARED_LIBS})
		SET(BUILD_SHARED_LIBS OFF)

		ADD_SUBDIRECTORY("${LIBSSH2_EMBEDDED_PATH}" "${libgit2_BINARY_DIR}/deps/libssh2")

		SET(BUILD_SHARED_LIBS ${BUILD_SHARED_LIBS_OLD})
		UNSET(BUILD_SHARED_LIBS_OLD)
		UNSET(BUILD_TESTING)

		LIST(APPEND LIBGIT2_LIBS libssh2)
		LIST(APPEND LIBGIT2_SYSTEM_INCLUDES "${LIBSSH2_EMBEDDED_PATH}/include")

		SET(BACKEND "embedded libssh (${LIBSSH2_EMBEDDED_PATH})")
	ELSE()
		MESSAGE(FATAL_ERROR "Asked for SSH backend ${SSH_BACKEND} but it wasn't found")
	ENDIF()

	SET(GIT_SSH 1)
	ADD_FEATURE_INFO(SSH ON "SSH transport using ${BACKEND}")
ELSE()
	SET(GIT_SSH 0)
	ADD_FEATURE_INFO(SSH OFF "SSH transport")
ENDIF()
